// c_minus.cup — parser da linguagem C-

import java_cup.runtime.Symbol;

parser code {:
  public static void main(String[] args) throws Exception {
    java.io.Reader r =
      (args.length == 0 || "-".equals(args[0]))
        ? new java.io.InputStreamReader(System.in)
        : new java.io.FileReader(args[0]);
    CMinusLexerCup lx = new CMinusLexerCup(r); // lexer “para CUP”
    CMinusParser p = new CMinusParser(lx);
    System.out.println(p.parse().value);
  }
:};

terminal ID, NUM, LPAREN, RPAREN, LBRAC, RBRAC, LBRACK, RBRACK, PLUS, MINUS, STAR, SLASH;
terminal LESSTHAN, LESSTHANEQ, GREATERTHAN, GREATERTHANEQ, EQUAL, NOTEQUAL, ASSIGN, SEMICOLON, COMMA;
terminal INT, ELSE, IF, RETURN, VOID, WHILE;

non terminal program, declaration_list, declaration, var_declaration, fun_declaration;
non terminal type_specifier, params, param_list, param;
non terminal compound_stmt, local_declarations, statement_list, statement;
non terminal expression_stmt, selection_stmt, iteration_stmt, return_stmt;
non terminal expression, var, simple_expression, relop, additive_expression, addop, term, mulop, factor, call, args, arg_list;

precedence nonassoc IF;
precedence nonassoc ELSE;

start with program;

program ::= declaration_list:dl;

declaration_list ::= declaration_list:dl declaration:dcl
    | declaration:dcl
    ;

declaration ::= var_declaration:vard
    | fun_declaration:fund
    ;

var_declaration ::= type_specifier:typspc ID SEMICOLON
    | type_specifier:typspc ID LBRACK NUM RBRACK SEMICOLON
    ;

type_specifier ::= INT
    | VOID
    ;

fun_declaration ::= type_specifier:typspc ID LPAREN params RPAREN compound_stmt:cs
    ;

params ::= param_list:pl
    | VOID
    ;

param_list ::= param_list:pl COMMA param:p
    | param:p
    ;

param ::= type_specifier:typspc ID
    | type_specifier:typspc ID LBRACK RBRACK
    ;

compound_stmt ::= LBRAC local_declarations:ld statement_list:sl RBRAC
    ;

local_declarations ::= local_declarations:ld var_declaration:vd
    | 
    ;

statement_list ::= statement_list:sl statement:s
    | 
    ;

statement ::= expression_stmt
    | compound_stmt
    | selection_stmt
    | iteration_stmt
    | return_stmt
    ;

expression_stmt ::= expression SEMICOLON
    | SEMICOLON
    ;

selection_stmt ::= IF LPAREN expression RPAREN statement
    | IF LPAREN expression RPAREN statement ELSE statement
    ;

iteration_stmt ::= WHILE LPAREN expression RPAREN statement
    ;

return_stmt ::= RETURN SEMICOLON
    | RETURN expression SEMICOLON
    ;

expression ::= var ASSIGN expression
    | simple_expression
    ;

var ::= ID
    | ID LBRACK expression RBRACK
    ;

simple_expression ::= additive_expression relop additive_expression
    | additive_expression
    ;

relop ::= LESSTHANEQ
    | LESSTHAN
    | GREATERTHAN
    | GREATERTHANEQ
    | EQUAL
    | NOTEQUAL
    ;

additive_expression ::= additive_expression addop term
    | term
    ;

addop ::= PLUS
    | MINUS
    ;

term ::= term mulop factor
    | factor
    ;

mulop ::= STAR
    | SLASH
    ;

factor ::= LPAREN expression RPAREN
    | var
    | call
    | NUM
    ;

call ::= ID LPAREN args RPAREN
       ;

args ::= arg_list
    |
    ;

arg_list ::= arg_list COMMA expression
    | expression
    ;